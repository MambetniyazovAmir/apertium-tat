
Alphabet

 А Ә Б В Г Д Е Ё Ж Җ З И Й К Л М Н Ң О Ө П Р С Т У Ү Ф Х Һ Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
 а ә б в г д е ё ж җ з и й к л м н ң о ө п р с т у ү ф х һ ц ч ш щ ъ ы ь э ю я
 %0 1 2 3 4 5 6 7 8 9
 %{A%}:а %{I%}:ы %{E%}:а %{U%}:у

 %{L%}:л %{G%}:г %{D%}:д %{K%}:к

 %{s%}:с %{j%}:0 %{w%}:0 %{n%}:0 %{l%}:л %{ъ%}:0 %{т%}:0 %{д%}:0 %{е%}:ы %{ь%}:0

 %>:0 % 

 !!! These are needed for the phonology of numbers, alphabetisms, and acronyms
 !!! They trigger relevant phonology and disappear
 %{а%}:0 %{э%}:0
 %{й%}:0 %{л%}:0 %{н%}:0 %{з%}:0 %{с%}:0
 !!! And this is currently only for Russian nouns with affixes containing back vowels
 %{☭%}:0 ;

Sets

BackVow    = А О Ы У
             а о ы у ;

FrontVow   = Ә Е Э И Ө Ү
             ә е э и ө ү;

UnambigFrontVow = Ә Э Ө Ү
                  ә э ө ү;

AmbigVow  = Я Е Ю
            я е ю ;

Vow       = А О Ы У Я Ю Ә Е Э И Ө Ү
            а о ы у я ю ә е э и ө ү;

BeginVow  = А О Ы У Ә Э И Ө Ү
            а о ы у ә э и ө ү ;

Cns       =  Б В Г Д Ж Җ З Й К Л М Н Ң П Р С Т Ф Х Һ Ц Ч Ш Щ
             б в г д ж җ з й к л м н ң п р с т ф х һ ц ч ш щ % 
             ү Ү у У ю Ю ;

NasalCns  =  М Н Ң
             м н ң ;

VoicedCns =  Б Д Г В З Ж
             б д г в з ж ;

CnsVoiceless = К П Т Ф Х Һ Ц Ч С Ш Щ
               к п т ф х һ ц ч с ш щ ;

RusVclsCns = Б Д Г В ! Жж and Зз keep voiceless consonants after them
             б д г в ;


Rules

!------------------------------------------------------------------------------------------------------------


"A Vowel Harmony"
%{A%}:ә <=> [ :FrontVow [ :Cns | :AmbigVow ]* :Cns ]/[ %>: | %  | [ :0 - [ %{ъ%}: | %{☭%}: ] ] ] _ ;
                                        [ :Cns ь: %>: :Cns* ]/[ :0 - [ %>: | %{ъ%}: | %{☭%}: ] ] _ ; ! stems ending in e.g. ль
  [ [ :и | :UnambigFrontVow | :ь ] :AmbigVow :Cns+ ]/[ %>: | %  | [ :0 - [ %{ъ%}: | %{☭%}: ] ] ] _ ;
  except
                                                  [ .#. :е :Cns+ ]/[ %>: | %{ъ%}: | %{☭%}: ] _ ;
                                                                  [ %{ъ%}: | %{☭%}: ] %>: ?* _ ;
                                               :BackVow [ ? - [ :FrontVow | %{ь%}: | ь: ] ]* _ ;
                       [ :BackVow (й:0) :AmbigVow :Cns+ ]/[ [ %>: | :0 ] - [ ь: | %{ь%}: ] ] _ ;
                                                            [ %{ъ%}: | %{☭%}: ] %>: ?* :Cns+ _ ; ! повестенда
                                                                                             _ (%>:) %{E%}:и ;
                                                                                             _ %>: %{E%}: ;
                                                                [ [ :Vow %{j%}: ] | й: ]/%>: _ ;
                                                                                     :Vow/:0 _ %{E%}:/:0 ;
                                                             [ :BackVow :Cns+ ]/[ %>: | :0 ] _ (%>:) %{E%}: ;
                                                        .#. [ :AmbigVow :Cns+ ]/[ %>: | :0 ] _ (%>:) %{E%}: ;
                                           [ [ %{ъ%}: | %{☭%}: ] %{I%}:е :Cns+ ]/[ :0 - ь: ] _ ; ! фасолена
 .#. [ :AmbigVow [ :Cns - [ :ү | :Ү ] ]+ ]/[ %>: | %{ъ%}: | %{☭%}: ] [ :Cns - [ :ү | :Ү ] ]* _ ; ! ермас


"Turn A to ы before E:й"
%{A%}:ы <=>                     [ :BackVow :Cns+ ]/[ %>: | :0 ] _ (%>:) [ %{E%}: - :и ] ;
                     [ :Cns+ [ %{ъ%}: | %{☭%}: ] ]/[ %>: | :0 ] _ (%>:) [ %{E%}: - :и ] ;
                           .#. [ :AmbigVow :Cns+ ]/[ %>: | :0 ] _ (%>:) [ %{E%}: - :и ] ;
                      [ :BackVow :AmbigVow :Cns+ ]/[ %>: | :0 ] _ (%>:) [ %{E%}: - :и ] ;
      .#. [ :AmbigVow :Cns+ ]/[ %>: | %{ъ%}: | %{☭%}: | :0 ] :м _ (%>:) %{E%}:й ; ! ермый
 [ :BackVow :AmbigVow :Cns+ ]/[ %>: | %{ъ%}: | %{☭%}: | :0 ] :м _ (%>:) %{E%}:й ; ! аермый
 except
     .#. [ :AmbigVow :Cns+ ]/[ %>: | %{ъ%}: | %{☭%}: | :0 ] :м _ (%>:) [ :с ] ; ! ерачак, ермас


"Convert {A} to я after й, и and {j}"
%{A%}:я <=> [ [ :Vow %{j%}: ] | й: | и: ]/%>: _ ;
                                .#. :д :и %>: _ ;
                                except
                                    _ ( %>: ) %{E%}: ;


"Delete {A} before E:и"
%{A%}:0 <=>      :Vow/:0 _ %{E%}:/:0 ;
                 :Cns/:0 _ [ %{E%}:и | %{E%}:й ]/:0 ;
                  %>: м: _ %>: %{A%}:ы %{E%}:й к: ; ! булдыр+ма+ыйк
                  %>: м: _ %>: %{A%}:? %{E%}:и к: ; ! кил+мә+ик
      %>: м: %{A%}:? %>: _ %{E%}:и к: ; ! кил+мә+ик
      except
          .#. [ :AmbigVow :Cns+ %{A%}:* ]/[ %>: | [ :0 - [ %{A%}: | FrontVow: ] ] ] _ ; ! ермыйк
                                          [ :BackVow :AmbigVow :Cns+ ]/[ %>: | :0 ] _ (%>:) [ %{E%}: - :и ] ;
                                      [ :BackVow :Cns* [ :Cns - й: ] ]/[ %>: | :0 ] _ ( %>: ) %{E%}: ;
                                      .#. [ :AmbigVow :Cns* [ :Cns - й: ] ]/[ %>: ] _ ( %>: ) %{E%}: ;
                                                       [ [ :Vow %{j%}: ] | й: ]/%>: _ ;
                                                                      .#. :д :и %>: _ ;


"Turn а to ы before E:й"
а:ы <=> _ [ %>: | %{A%}:0 ]* %{E%}:й ;
        except
            [ %{ь%}: ] %>: ?* _ ;


"Delete ә before E:и"
ә:0 <=> _ [ %>: | %{A%}:0 ]* %{E%}:и ;


!------------------------------------------------------------------------------------------------------------


"Turn {E} into й after a vowel"
%{E%}:й <=> [ :Vow - :FrontVow ]/[ %>: | :0 ] _ ;
            except
                [ [ :FrontVow - :е ] :Cns+ %{A%}: ]/[ %>: | [ :0 - [ %{A%}: | FrontVow: ] ] ] _ ;
               [ :FrontVow :AmbigVow :Cns+ %{A%}: ]/[ %>: | [ :0 - [ %{A%}: | FrontVow: ] ] ] _ ; ! иярмик
                [ [ е: | ә: ] [ %{ь%}: | %{ъ%}: | %{☭%}: ]* ]/[ %>: | :0 | [ %>: | %{A%}: ] ] _ ;
                                                                               FrontVow:0 %>: _ ;
                                                                 [ [ :Vow %{j%}: ] | й: ]/%>: _ ;
                                                                                       :у %>: _ ;


"Turn {E} into и to combine with ә and е"  ! сөйлә>{E}:сөйли
                                           ! төзе>{E}:төзи
%{E%}:и <=>          [ [ е: | ә: ] [ %{ь%}: ]* ]/[ %>: | :0 | [ %>: | %{A%}: ] ] _ ;
            [ :FrontVow :Cns+ %{A%}: ]/[ %>: | [ :0 - [ %{A%}: | FrontVow: ] ] ] _ ;
                                                        :FrontVow :й %>: %{A%}:0 _ [ :м | :к ] ; ! җәйик
                                                                  :и %>: %{A%}:0 _ ; ! киик
                                                      %>: м: %{A%}:0 %>: %{A%}:0 _ [ :к | :м ] ; ! кил+мә+ик
            except
                                         [ %{ъ%}: | %{☭%}: ] %>: ?* _ ;
                :BackVow [ ? - [ :FrontVow | %{ь%}: | ь: | ә:0 ] ]* _ ;
                                       [ [ :Vow %{j%}: ] | й: ]/%>: _ ;
                                                      .#. :д :и %>: _ ;
      [ :FrontVow :Cns+ ]/[ %>: | [ :0 - [ %{A%}: | FrontVow: ] ] ] _ ;


"E Vowel Harmony"
%{E%}:ә <=> [ :FrontVow :Cns+ ]/[ %>: | [ :0 - [ %{A%}: | %{ъ%}: | %{☭%}: ] ] ] _ ;
            except
                                                                  [ %{ъ%}: | %{☭%}: ] %>: ?* _ ;
                                               :BackVow [ ? - [ :FrontVow | %{ь%}: | ь: ] ]* _ ;
                                                                       [ :Vow ]/[ %>: | :0 ] _ ;
                                                                         %{A%}:/[ %>: | :0 ] _ ;
                                 [ [ е: | ә: ] [ %{ь%}: ]* ]/[ %>: | :0 | [ %>: | %{A%}: ] ] _ ;
                                                                [ [ :Vow %{j%}: ] | й: ]/%>: _ ;
                                             [ :BackVow (й:0) :AmbigVow :Cns+ ]/[ %>: | :0 ] _ ;
 .#. [ :AmbigVow [ :Cns - [ :ү | :Ү ] ]+ ]/[ %>: | %{ъ%}: | %{☭%}: ] [ :Cns - [ :ү | :Ү ] ]* _ ; ! ера


"Convert {E} to я after й, и and {j}"
%{E%}:я <=> [ [ :FrontVow %{j%}: ] | й: | и: ]/%>: _ ;


"Epenthetic {e} Vowel Harmony"
%{е%}:е <=> [ :FrontVow :Cns+ ]/[ %>: | %  | [ :0 - [ %{ъ%}: | %{☭%}: ] ] ] _ ;
            _ %>: %{s%}: ;
            except
                _ :Cns %>: :0* :Vow ;
                _ :Cns %>: %{s%}: ;
                _ :Cns %{е%}: %>: %{s%}: ;
                _ :Cns %{е%}: %>: %{I%}: ;
                _ .#. ;
                _ %>: [ ? - %{s%}: ] ;


"Epenthetic {е} Deletion"
%{е%}:0 <=> _ :Cns %>: :0* :Vow ;               ! ур{e}н>{s}{I}{n}:урны
            _ :Cns %>: %{s%}: ;                 ! ур{e}н>{s}{I}{n}>{D}{A}н:урнынан
            _ :Cns %{е%}: %>: (%{s%}:) %{I%}: ; ! сең{е}л{е}>{I}м:сеңлем
            _ .#. ;                             ! сең{е}л{е}:сеңел
            _ %>: [ ? - %{s%}: ] ;              ! сең{е}л{е}>{G}{A}:сеңелгә
            except
                _ %>: %{s%}: ;


"Delete е before E:и"
е:0 <=> _ [ %>: | %{A%}:0 ]* %{E%}:и ;


!------------------------------------------------------------------------------------------------------------


"I Vowel Harmony"
%{I%}:е <=>                                       [ :Cns ь:0 [ %{ъ%}: | %{☭%}: ] ]/[ %>: | :0 ] _ ; ! повесте
        [ :FrontVow [ :Cns - [ :у | :У ] ]+ ]/[ %>: | %  | [ :0 - [ %{ъ%}: | %{☭%}: | ъ:0 ] ] ] _ ;
                                                                       [ й: | и: ]/[ %>: | :0 ] _ ;
                                       [ :Cns ь: %>: :Cns* ]/[ :0 - [ %>: | %{ъ%}: | %{☭%}: ] ] _ ;
 [ [ :и | :UnambigFrontVow | :ь ] :AmbigVow :Cns+ ]/[ %>: | % | [ :0 - [ %{ъ%}:0 | %{☭%}: ] ] ] _ ;
                                            [ %{ь%}:0 :Cns* ]/[ [ :0 - [ %{ь%}:0 | %{☭%}: ] ] ] _ ;
                                                          :FrontVow :AmbigVow :Cns+ %>: %{s%}:0 _ ; ! Хәяте
                                             [ [ :Vow %{j%}: ] | й: ]/[ %>: | [ :0 - %{j%}: ] ] _ ; ! ай>{s}{I}:ае
                                                                                                    ! ди{j}>{I}п:дип
                                                                                                    ! ди{j}>{I}{l}>{G}{A}н:диелгән
                                                                                  .#. :д :и %>: _ ; ! ди{j}>{E}:ди
 except
 .#. [ :AmbigVow [ :Cns - [ :ү | :Ү ] ]+ ]/[ %>: | %{ъ%}: | %{☭%}: ] [ :Cns - [ :ү | :Ү ] ]* _ ; ! ерсын
                       [ :BackVow (й:0) :AmbigVow :Cns+ ]/[ [ %>: | :0 ] - [ ь: | %{ь%}: ] ] _ ; ! муены, but not җәмгыяте
                                                            [ %{ъ%}: | %{☭%}: ] %>: ?* :Cns+ _ ; ! повестеның
                                                                                 [ ъ:0 ] %>: _ ; ! шәфәгы
                                                                              %{n%}: %>: н:0 _ .#. ;
                                    [? - :д ] [ :Vow - [ :Cns | :и ] ]/[ [ :0 - й: ] | %>: ] _ ; ! avoid conflict with {I} deletion
                                                                               .#. :д :и %>: _ [ :п | :ң ] ;


"Delete accusative {I} after px3"
%{I%}:0 <=> %{n%}: %>: н:0 _ .#. ;


"Deletion of {I} after vowels"
%{I%}:0 <=> [ :Vow - :Cns ]/[ [ :0 - й: ] | %>: ] _ ;
                                    .#. :д :и %>: _ [ :п | :ң ];
            except
                                       %{n%}:/:0 _ ;
                             [ й: ]/[ %>: | :0 ] _ ;
                  [ ? - :д ] [ и: ]/[ %>: | :0 ] _ ;
              [ [ ? - .#. ] :д и: ]/[ %>: | :0 ] _ ;
                                   .#. :д :и %>: _ [ ? - [ :п | :ң ] ];


!------------------------------------------------------------------------------------------------------------


"Fronting of U"
%{U%}:ү <=> [ :FrontVow :Cns+ ]/[ %>: | :0 ] _ ; ! бел>{U}:белү
                  [ :FrontVow ]/[ %>: | :0 ] _ ; ! сөйлә>{U}:сөйләү
            except
                .#. [ :AmbigVow [ :Cns - [ :ү | :Ү ] ]+ ]/[ %>: | %{ъ%}: | %{☭%}: ] [ :Cns - [ :ү | :Ү ] ]* _ ; ! еру
                                                                            [ %{ъ%}: | %{☭%}: | ъ: ] %>: ?* _ ;
                                                              :BackVow [ ? - [ :FrontVow | %{ь%}: | ь: ] ]* _ ;
                                                                                      [ :Vow ]/[ %>: | :0 ] _ [ :Vow ]/[ %>: | :0 ] ;
                                                                  [ :BackVow :AmbigVow :Cns+ ]/[ %>: | :0 ] _ ; ! җыел>{U}>{s}{I}:җыелуы
                                                                                            [ й: | и: ] %>: _ ;
! кайт>{U}:кайту


"U > в between vowels"
%{U%}:в <=> [ :Vow - [ :у | :ү ] ]/[ %>: | :0 ] _ [ :Vow ]/[ %>: | :0 ] ;
            except
                [ й: | и: ] %>: _ ;
! сөйлә>{U}>{s}{I}н:сөйләвен
! уйна>{U}>{s}{I}н:уйнавы
! but not ту:тууы


"U > ю after й"
%{U%}:ю <=> [ й: | и: ] %>: _ ;
! сөй+у:сөю, куй+у:кую, ти+ү:тию


"word-final у or ү becomes в when a vowel-initial suffix follows and a vowel precedes"
Vu:в <=> [ :Vow - [ :у | :ү ] ]/:0 _ %>: [ :Vow | %{U%}: ]/[ :0 ] ;
         where Vu in ( у ү ) matched;
! яу, яварга, яву
! but not суы:свы, тууы:тувы !!


"Deletion of high vowels before {U}"
Vi:0 <=> _ ( %>: ) %{U%}: ;
         where Vi in ( е ы ) matched ;
! укы>{U}>{s}{I}{n}:укуы
! төзе>{U}>{s}{I}{n}:төзүе


!------------------------------------------------------------------------------------------------------------


"Voicing of к/п"
! intervocalic:          ! before -рак/-рәк
!! китап>{c}{I}          !! кызык>рак !! кысып>рак
!! китабы                !! кызыграк  !! кысыбрак
Cx:Cy <=> :Vow _ [ %{ь%}: | %{ъ%}: | %{☭%}: | ь: | ъ: ]* %>: ( :0 ) :Vow ;
               _ %>: :р ;
               except
                         .#. х: а: _ %>: %{s%}:0* %{I%}:ы :* ; ! хакы, хакын
                   .#. х: о: к: у: _ %>: %{s%}:0* %{I%}:ы :* ; ! хокукы, хокукын
                   where Cx in ( п к )
                         Cy in ( б г )
                         matched;


"Forward voicing assimilation"
Cx:Cy <=> :CnsVoiceless/[ %>: | %  | :0 | :ь | :ъ ] _ ;
              [ :RusVclsCns ]/[ :0 | %  | :ь | :ъ ] _ ;
              where Cx in ( %{G%} %{D%} )
                    Cy in (   к     т   )
                    matched;


!------------------------------------------------------------------------------------------------------------


"Deletion of {s} after a consonant"
%{s%}:0 <=> [ :Cns | %{j%}:0 | %{w%}:0 | ь: | й:0 | и: ]/[ %>: | :0 ] _ ;
            except
                %{е%}: %>: _ ;


!------------------------------------------------------------------------------------------------------------
!!!! possessive+case phonology !!!!


"extra n appears before locative, dative, ablative, accusative"
%{n%}:н <=> _ [ [ %{D%}: | %{G%}: ] %{A%}: ]/[ %>: | :0 ] ;
            _ %>: н: %{I%}: .#. ;
!елында, елына, елыннан, елын


"ablative D to н between nasal codas"
%{D%}:н <=> :NasalCns [ %{ь%}: | %{ъ%}: | %{☭%}: | ъ: | ь: ]* %>:0 _ %{A%}: :н ;
!елыннан, елымнан, елыңнан


"delete accusative н after px3"
н:0 <=> %{n%}: %>: _ %{I%}: .#. ;


!------------------------------------------------------------------------------------------------------------


"Plural alternation after nasal"
%{L%}:н <=> :NasalCns/[ %>: | :0 ] _ %{A%}: р ;


"Passive {l} becomes n after verb stem ending in /l/"
%{l%}:н <=> [ л :Vow ]/[ :0 | %>: ] _ ;


!------------------------------------------------------------------------------------------------------------


"Delete й in й>е and й>я and й>ю"
й:0 <=> _ [ %{ъ%}: | %{☭%}: ]* %>: :AmbigVow/:0 ;


!------------------------------------------------------------------------------------------------------------


"я to й before %{A%}"
я:й <=> _ ( %>: ) %{A%}: ;


!------------------------------------------------------------------------------------------------------------
!!!! Special archiphonemes !!!!


"Deletion of dative {G} after px1sg, px2sg"
%{G%}:0 <=>  %{I%}: :NasalCns %>: _ %{A%}: .#. ;
                      %{n%}:н %>: _ %{A%}: ; ! берсенә


!------------------------------------------------------------------------------------------------------------
!!!!!!!! To be improved !!!!!!!!!!!!!!!!!!!!!!!!
! allow analysis of "дусы", "асы" etc      !!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


"{д} surfaces before vowels"
! ал%{д%}+{D}{A}н     ал%{д%}+{I}
! ал-дан               алд-ы
%{д%}:д <=> _ :Vow/[ %>: | :0 ] ;


"{т} surfaces before vowels"
%{т%}:т <=> _ :Vow/[ %>: | :0 ] ;
! ас%{т%}+{D}{A}н     ас%{т%}+{I}
! ас-тан              аст-ы


"Soft sign deletion before suffix starting with a vowel"
ь:0 <=> _ [ %{ъ%}: | %{☭%}: ]* %>: :Vow/[ :0 - %>: ] ;

"Hard sign deletion before suffix starting with a vowel"
ъ:0 <=> _ [ %{ъ%}: | %{☭%}: ]* %>: :Vow/[ :0 - %>: ] ;
